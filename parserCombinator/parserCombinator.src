import_code("../StringUtils/StringUtils.src":"../StringUtils/StringUtils.src")

ParserState={}
ParserState.targetString = ""
ParserState.result = null
ParserState.index = 0
ParserState.isError = false
ParserState.error = null

updateParserState = function(state,index,result)
    newState = new ParserState
	newState.targetString = state.targetString
	newState.result = result
    newState.index = index
    newState.isError = state.isError
    newState.error = state.error
    return newState
end function

updateParserError = function(state,errorMessage)
    newState = new ParserState
	newState.targetString = state.targetString
	newState.result = state.result
    newState.index = state.index
    newState.isError = true
    newState.error = errorMessage
    return newState
end function

stringParser = function(input_str)
    parser = function(parserState)

    if parserState.isError then
        return parserState
    end if
    slicedTarget = slice(parserState.targetString,parserState.index)
    if slicedTarget.len == 0 then
        return updateParserError(parserState,"str: Tried to match : "+input_str+" , but got unexpected end of input.")
    end if
    if stringStartsWith(slicedTarget,input_str) then
        parserStateTwo = updateParserState(parserState,parserState.index+input_str.len,input_str)
        return parserStateTwo
    end if
        parserStateTwo = updateParserError(parserState, "tried to match "+input_str+", but got : "+slice(parserState.targetString,parserState.index,parserState.index+10)+"")
        return parserStateTwo
    end function
    parserObj = Parser.construct(@parser)
    return parserObj
end function

sequenceOfParser = function(parsers)
    parser = function (parserState)
        if parserState.isError then
            return parserState
        end if 
        results = []
        nextState = parserState

        for p in parsers
            nextState = p.parserStateTransformerFunction(nextState)
            if nextState.isError then
                break
            end if
            results.push(nextState.result)
        end for

        lastParserState = updateParserState(nextState,nextState.index,results)
        return lastParserState
    end function
    parserObj = Parser.construct(@parser)
    return parserObj
end function

Parser = {}
Parser.construct = function(parserStateTransformerFunction)
    self = new Parser
    self.parserStateTransformerFunction = @parserStateTransformerFunction
    return self
end function
Parser.run = function(targetString)
    initialState = new ParserState
    initialState.targetString = targetString
    initialState.result = null
    initialState.index = 0
    initialState.isError = false
    initialState.error = null
    return self.parserStateTransformerFunction(initialState)
end function
